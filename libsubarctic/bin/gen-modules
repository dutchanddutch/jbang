#!/usr/bin/perl

use v5.14;
use strict;
use warnings qw( FATAL all );
use experimental qw( signatures );
use autodie;

# mangle a global data symbol
sub mangle ($sym) {
	$sym =~ /::/  or return $sym;

	my $std = $sym =~ s/^std::// && 'St';
	my @sym = map length . $_, split /::/, $sym;

	local $" = '';
	@sym > 1 ? "_ZN${std}@{sym}E" : "_Z${std}${sym}"
}

open my $mkfh, '>', 'modules.mk';

while( <> ) {
	chomp;
	my( $sym, $addr, $include ) = split /\t/;

	if( $addr !~ /000\z/ ) {
		# I'm lazy
		warn "ignoring non-page-aligned $sym for now\n";
		next;
	}

	my $mod = $sym;
	$mod =~ s/::/-/g;
	$mod =~ s/(?<![^-])hw-(\w+)\z/$1/ or die;

	my $msym = mangle $sym;

	my $init = "init_$mod";
	$init =~ s/-/_/g;

	open my $fh, '>', "hw-$mod.cc";
	print $fh <<"END";
#include "$include"
#include "map-phys.h"

[[ gnu::constructor(102) ]]
let static $init() -> void {
	let static constexpr size = -(-sizeof $sym & -0x1000u);

	asm( ".comm $msym, %c0, %c1" ::
		"n"(size),
		"n"(max( alignof $sym, 0x1000u )) );
	map_phys( &$sym, 0x$addr, size );
}
END
	close $fh;

	print $mkfh "modules += hw-$mod.o\n";
}
